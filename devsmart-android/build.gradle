buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 19
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }


    buildTypes {
        release {
        }
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }
}

dependencies {
    compile project(':devsmart-core')
    compile 'com.android.support:support-v4:21.0.3'
    compile 'org.slf4j:slf4j-api:1.7.8'

}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts() {
    archives androidSourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {

        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            name 'Devsmart-core'
            description 'Collection of utilities for for Android.'
            url 'http://github.com/dinocore1/DevSmartCore'

            scm {
                url 'scm:git@github.com:dinocore1/DevSmartCore.git'
                connection 'scm:git@github.com:dinocore1/DevSmartCore.git'
                developerConnection 'scm:git@github.com:dinocore1/DevSmartCore.git'
            }

            licenses {
                license {
                    name 'MIT License'
                    url 'https://github.com/dinocore1/DevSmartCore/blob/master/LICENSE.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'psoucy'
                    name 'Paul Soucy'
                }
            }
        }

        //repository(url: "file:///home/paul/mymaven")
        //repository(url: "file://localhost/var/www/maven/")
        //pom.groupId = project.group
        //pom.artifactId = 'miniweb-android'
        //pom.version = project.version
        //pom.packaging = 'aar'

    }
}
